/* 
  Raphael Barcellos Pereira
  Licensed for non-commercial use, must include this license message
  IFRJ/CPar
  Essa sketch grava e identifica digitais
 
 Alterado para a versão GT-511C1 e corrigido por Raphael Barcellos 
 */

int rec = 6;//Botao 1
int zero = 7;//Botao 2
int rele = 8;//Botao 2

int temp = 0;//Comparador

#include "FPS_GT511C3.h"
#include "SoftwareSerial.h"
#include <LiquidCrystal.h> //Inclui a biblioteca do LCD

// Hardware setup - FPS connected to:
//   digital pin 4(arduino rx, fps tx)
//   digital pin 5(arduino tx, fps rx)

LiquidCrystal lcd(2, 3, 9, 10, 11, 12); //Configura os pinos do Arduino para se comunicar com o LCD
FPS_GT511C3 fps(4, 5);//Configura os tx e rx scanner

void setup()
{

  lcd.begin(16, 2); //Inicia o LCD com dimensões 16x2(Colunas x Linhas)
  delay(100);
  fps.Open();
  fps.SetLED(true);
  //Define pinos como entrada
  pinMode(rec, INPUT);
  pinMode(zero, INPUT);
  pinMode(rele, OUTPUT);
}

void loop()
{
  //Faz a leitura dos botoes e grava seus estados
  int rec1 = digitalRead(rec);
  int zero1 = digitalRead(zero);

  //Requerindo o cadastro de novas digitais
  if(rec1==0)
  {
    //Printa as informações no LCD
    lcd.clear();// Limpa do buffer do LCD
    lcd.setCursor(0, 0);
    lcd.print("Novo cadastro");
    lcd.setCursor(0, 1);
    lcd.print("Aguarde");
    delay(2000);//Aguarda 2 segundos
    lcd.clear();// Limpa do buffer do LCD
    lcd.setCursor(0,0);
    lcd.print("Posicione o dedo");
    lcd.setCursor(0, 1);
    lcd.print("no Scanner Cad.");

    //Chama a função que faráos registros de novas digitais
    Enroll();
  }

  //Zerando o registro de digitais
  if(zero1==0)
  {
    fps.DeleteAll();// Apaga todo o registro de memoria de digitais
    //Printa as informações no LCD
    lcd.clear(); // Limpa do buffer do LCD
    lcd.setCursor(0, 0);
    lcd.print("O Registro foi");
    lcd.setCursor(0, 1);
    lcd.print("Apagado");
    delay(2000);//Aguarda 2 segundos
  }

  // Identificando digitais
  if (fps.IsPressFinger())
  {
    fps.CaptureFinger(false);//Realiza a leitura da digital no scanner
    int id = fps.Identify1_N();//Salva na variavel id o codido de identificação da digital
    if (id <20)//Compara se é menor que 20, no caso do modelo GT-511C1 voce consegue armazenar ate 20 digitais diferentes
      // Ou seja se for menor é porque esta cadastrado
    {
      //Printa as informações no LCD se houver reconhecimento de dados
      lcd.clear();// Limpa do buffer do LCD
      lcd.setCursor(0, 0);
      lcd.print("Liberado ID:");
      lcd.setCursor(13,0);
      lcd.print(id);
      lcd.setCursor(0, 1);
      lcd.print("Bem Vindo");
      digitalWrite(rele,1);
      delay(1000);//Aguarda 1 segundos
      digitalWrite(rele,0);
      delay(1000);//Aguarda 1 segundos
    }
    else
    {
      //Printa as informações no LCD se nao houve reconhecimento de dados
      lcd.clear();// Limpa do buffer do LCD
      lcd.setCursor(0, 0);
      lcd.print("Nao reconhecido");
      lcd.setCursor(0, 1);
      lcd.print("ID Nao Cadast.");
      delay(1500);//Aguarda 2 segundos

    }
  }
  else
  {
    //Printa as informações no LCD se nao houve reconhecimento de dados
    lcd.setCursor(0, 0);
    lcd.print("Pressione o dedo");
    lcd.setCursor(0, 1);
    lcd.print("no Scanner ");
  }
}


//Funcao responsavel por cadastar impressoes digitais
void Enroll()
{
  int enrollid = 0;
  //É verificado quantos digitais tem cadastradas, e selecionandoa proxima posição disponivel para armazenamento
  //======================================================================================
  bool usedid = true;
  while (usedid == true)
  {
    usedid = fps.CheckEnrolled(enrollid);
    if (usedid==true) enrollid++;
  }
  fps.EnrollStart(enrollid);
  temp=enrollid;
  //======================================================================================

  //Aguarda até que o dedo seja retirado do scanner
  while(fps.IsPressFinger() == false) delay(100);
  bool bret = fps.CaptureFinger(true);
  int iret = 0;
  //Se conseguir realizar alguma leitura a variavel "bret" valerá 1
  if (bret != false)
  {
    //Se for diferente de 0 irá printar a informação para que o dedo seja removido do scanner
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Remova o dedo");
    fps.Enroll1();//Salva a primeira informacao da digital
    //Aguarda até que o dedo seja retirado do scanner
    while(fps.IsPressFinger() == true) delay(100);
    lcd.clear();//Limpa o buffer do LCD
    //Solicita que o dedo seja pressionado no scanner novamente
    lcd.setCursor(0, 0);
    lcd.print("Pressione o dedo");
    lcd.setCursor(0, 1);
    lcd.print("Novamente");
    //Aguarda até que o dedo seja retirado do scanner
    while(fps.IsPressFinger() == false) delay(100);

    //Repete tudo por mais 2 vezes
    //=====================================================================================
    bret = fps.CaptureFinger(true);
    if (bret != false)
    {
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Remova o dedo");
      fps.Enroll2();
      while(fps.IsPressFinger() == true) delay(100);
      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print("Pressione o dedo");
      lcd.setCursor(0, 1);
      lcd.print("Novamente");
      while(fps.IsPressFinger() == false) delay(100);
      //====================================================================================
      //====================================================================================
      bret = fps.CaptureFinger(true);
      if (bret != false)
      {
        lcd.clear();
        lcd.setCursor(0, 0);
        lcd.print("Remova o dedo");
        iret = fps.Enroll3();
        //===================================================================================
        //Nessa parte é verificado se se o cadastro foi realizado com sucesso
        enrollid=0;          
        usedid = true;
        while (usedid == true)
        {
          usedid = fps.CheckEnrolled(enrollid);
          if (usedid==true) enrollid++;
        } 
        //Compara se o dedo foi pressionado 3 vezes e se a posição anterior da memoria é igualk a atual
        if (iret == 0&&temp!=enrollid)
        {
          //Se for igual printará as informações no LCD e salva automaticamente na memória
          lcd.clear();//Limpa o buffer do LCD
          lcd.setCursor(0, 0);
          lcd.print("Cadastro feito");
          lcd.setCursor(0, 1);
          lcd.print("Com sucesso");
          lcd.setCursor(13, 1);
          lcd.print(enrollid);
          delay(1000);//Aguarda 1 segundo
        }
        else
        {
          //Se não printará uma informação acusando falha
          lcd.clear();//Limpa o buffer do LCD
          lcd.setCursor(0, 0);
          lcd.print("Cadastro falho");
          lcd.setCursor(0, 1);
          lcd.print("Tente novamente");
          delay(1000);//Aguarda 1 segundo

        }
      }
      // Se houver falhas durante a captura printará umas das 3 mensagens abaixo, de acordo com cada posição
      //Erro 1
      else {
        lcd.clear();
        lcd.setCursor(0, 0);
        lcd.print("Falha na captura");
        lcd.setCursor(0, 1);
        lcd.print("Do primeiro dedo");
        delay(1000);
      }
      //====================================================================================
    }
    //Erro 2
    else {
      lcd.clear();
      lcd.setCursor(0, 0); //Posiciona o cursor na primeira coluna(0) e na primeira linha(0) do LCD
      lcd.print("Falha na captura"); //Escreve no LCD "Olá Garagista!"
      lcd.setCursor(0, 1); //Posiciona o cursor na primeira coluna(0) e na primeira linha(0) do LCD
      lcd.print("Do segundo dedo");
      delay(1000);
    }
    //=====================================================================================
  }
  //Erro 3
  else {
    lcd.clear();
    lcd.setCursor(0, 0); //Posiciona o cursor na primeira coluna(0) e na primeira linha(0) do LCD
    lcd.print("Falha na captura"); //Escreve no LCD "Olá Aluno!"
    lcd.setCursor(0, 1); //Posiciona o cursor na primeira coluna(0) e na primeira linha(0) do LCD
    lcd.print("Do terceiro dedo");
    delay(1000);
  }
  //======================================================================================
}
